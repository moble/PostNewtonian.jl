# This a utility that allow us to interoperate with FastDifferentiation.Node and other
# Number types.
function _efficient_vector(::Val{N}, ::Val{T}) where {N,T}
    if isbitstype(T)
        MVector{N, T}(undef)
    else
        Vector{T}(undef, N)
    end
end


@doc raw"""
    PNExpansion{N,T,NMax}

This object can be multiplied by a scalar or another `PNExpansion` object, and contains a
tuple of coefficients.  The coefficients are stored in the order of the expansion, with the
zeroth-order coefficient first.  Multiplication by a scalar just multiplies each of the
elements.  Multiplication by another `PNExpansion` object is done by convolving the two
tuples of coefficients.

Blanchet (2014) defines the post-Newtonian expansion parameter as follows:

> This parameter represents essentially a slow motion estimate ``ğœ– âˆ¼ ğ‘£/ğ‘``, where ``ğ‘£``
> denotes a typical internal velocity.  By a slight abuse of notation, following
> Chandrasekhar et al. [...], we shall henceforth write formally ``ğœ– â‰¡ 1/ğ‘``, even though
> ``ğœ–`` is dimensionless whereas ``ğ‘`` has the dimension of a velocity. Thus, ``1/ğ‘ â‰ª 1``
> in the case of post-Newtonian sources. The small post-Newtonian remainders will be denoted
> ``ğ’ª(1/ğ‘^ğ‘›)``. Furthermore, [...] we shall refer to a small post-Newtonian term with
> formal order ``ğ’ª(1/ğ‘^ğ‘›)`` relative to the Newtonian acceleration in the equations of
> motion, as ``\frac{ğ‘›}{2}\text{PN}``.

Therefore, we consider the coefficients of the `PNExpansion` to be a polynomial in ``1/ğ‘``.
Here, the type parameter `N` corresponds to the number of elements actually present in the
tuple of coefficients, and `T` is the type of the coefficients.  The `NMax` parameter is the
maximum number of elements, related to the usual PN order by
```math
\text{pn_order} = \frac{\texttt{NMax}-1} {2}.
```
The `N` parameter is not related to the PN order; it is just used by Julia to know how many
elements are currently in the coefficients, but is required to be 1 â‰¤ N â‰¤ NMax.

"""
struct PNExpansion{N,T,NMax}
    coeffs::NTuple{N,T}

    function PNExpansion{N,T,NMax}(coeffs) where {N,T,NMax}
        if N < 1
            throw(ArgumentError("`N=$N` must be >0."))
        end
        if N > NMax
            throw(ArgumentError("`N=$N` must be <`NMax=$NMax`."))
        end
        new{N,T,NMax}(coeffs)
    end
    function PNExpansion(coeffs::NTuple{N,T}, NMax) where {N,T}
        if N < 1
            throw(ArgumentError("`N=$N` must be >0."))
        end
        if N > NMax
            throw(ArgumentError("`N=$N` must be <`NMax=$NMax`."))
        end
        new{N,T,NMax}(coeffs)
    end
end

pn_order(::PNExpansion{N,T,NMax}) where {N,T,NMax} = (NMax - 1) // 2

Base.getindex(pn::PNExpansion, i::Int) = pn.coeffs[i]
Base.length(pn::PNExpansion) = length(pn.coeffs)
Base.eltype(pn::PNExpansion) = eltype(pn.coeffs)

function Base.sum(pn_expansion::PNExpansion{N,T,NMax}) where {N,T,NMax}
    sum(pn_expansion[i] for i âˆˆ 1:N, init = zero(T))
end

function Base.:+(pn::PNExpansion{N,T1,NMax}, x::T2) where {N,T1,NMax,T2<:Number}
    T3 = promote_type(T1, T2)
    PNExpansion(ntuple(i -> i == 1 ? pn[1] + x : T3(pn[i]), Val(N)), NMax)
end
Base.:+(x::T, pn::PNExpansion) where {T<:Number} = pn + x

function Base.:-(pn::PNExpansion{N,T,NMax}) where {N,T,NMax}
    PNExpansion{N,T,NMax}((-).(pn.coeffs))
end

function Base.:*(pn::PNExpansion{N,T1,NMax}, x::T2) where {N,T1,NMax,T2<:Number}
    T3 = promote_type(T1, T2)
    PNExpansion{N,T3,NMax}(@. T3(pn.coeffs * x))
end
Base.:*(x::T, pn::PNExpansion) where {T<:Number} = pn * x

function Base.:+(pn1::PNExpansion{N1,T1,NMax1}, pn2::PNExpansion{N2,T2,NMax2}) where
{N1,N2,T1,T2,NMax1,NMax2}
    throw(ArgumentError(
        "`PNExpansion` addition is only defined for objects of the same PN order."
        *
        "\nGot NMax1=$(NMax1) and NMax2=$(NMax2)."
    ))
end

function Base.:+(pn1::PNExpansion{N1,T1,NMax}, pn2::PNExpansion{N2,T2,NMax}) where
{N1,N2,T1,T2,NMax}
    if N1 > N2
        return pn2 + pn1
    else
        PNExpansion(ntuple(i -> sum_term(i, pn1, pn2), Val(N2)), NMax)
    end
end

function sum_term(i, pn1::PNExpansion{N1,T1,NMax}, pn2::PNExpansion{N2,T2,NMax}) where
{N1,N2,T1,T2,NMax}
    T3 = promote_type(T1, T2)
    if i â‰¤ N1
        return T3(pn1.coeffs[i] + pn2.coeffs[i])
    else
        return T3(pn2.coeffs[i])
    end
end

function Base.:*(pn1::PNExpansion{N1,T1,NMax1}, pn2::PNExpansion{N2,T2,NMax2}) where
{N1,N2,T1,T2,NMax1,NMax2}
    throw(ArgumentError(
        "`PNExpansion` multiplication is only defined for objects of the same PN order."
        *
        "\nGot NMax1=$(NMax1) and NMax2=$(NMax2)."
    ))
end

function Base.:*(pn1::PNExpansion{N1,T1,NMax}, pn2::PNExpansion{N2,T2,NMax}) where
{N1,N2,T1,T2,NMax}
    if N1 > N2
        return pn2 * pn1
    else
        N3 = min(N1 + N2 - 1, NMax)
        PNExpansion(ntuple(i -> product_term(i, pn1, pn2), Val(N3)), NMax)
    end
end

function product_term(i, pn1::PNExpansion{N1,T1,NMax}, pn2::PNExpansion{N2,T2,NMax}) where
{N1,N2,T1,T2,NMax}
    T3 = promote_type(T1, T2)
    sum(
        pn1.coeffs[j] * pn2.coeffs[i-j+1] for j âˆˆ max(1, i - N2 + 1):min(i, N1),
        init = zero(T3)
    )
end

Base.:/(p::PNExpansion, x::Number) = p * (1 / x)

function FastDifferentiation.derivative(
    pn_expansion::PNExpansion{N,T,NMax},
    fd_node::FastDifferentiation.Node
) where {N,T,NMax}
    PNExpansion(
        ntuple(
            i -> FastDifferentiation.derivative(pn_expansion[i], fd_node),
            Val(N)
        ),
        NMax
    )
end

Base.Tuple(pn::PNExpansion) = pn.coeffs
SVector(pn::PNExpansion) = SVector(pn.coeffs)


"""
    PNTerm{T,PNOrder,câ»Â¹Exponent}

This object represents a single term in a PNExpansion.  It has a single field: `coeff`,
which is the coefficient of the term.  The type parameter `T` is the type of the
coefficient.  The type parameter `PNOrder` is a half-integer (just as in
[`PNSystem`](@ref)s) representing the PN order of the expansion.  And the type parameter
`câ»Â¹Exponent` is an integer representing the exponent of the PN expansion parameter ``1/c``.

`PNTerm`s can be multiplied and divided by scalars and exponentiated by integers, to produce
another `PNTerm`.  They can also be added to other `PNTerm`s to produce a `PNExpansion`.

A simple way to define a `PNTerm` or a `PNExpansion` is to define the PN expansion parameter
```julia
c = PNExpansionParameter(pnsystem)
```
and use that naturally in formulas, as in
```julia
e = 1 + (v/c)^2 * (-Î½/12 - 3//4) + (v/c)^4 * (-Î½^2/24 + 19Î½/8 - 27//8)
```
Any exponent higher than the desired `PNOrder` will be automatically set to zero.

"""
struct PNTerm{T,PNOrder,câ»Â¹Exponent}
    coeff::T

    function PNTerm{T,PNOrder,câ»Â¹Exponent}(coeff) where {T,PNOrder,câ»Â¹Exponent}
        if câ»Â¹Exponent > 2PNOrder
            coeff = zero(coeff)
        end
        new{T,PNOrder,câ»Â¹Exponent}(coeff)
    end
    function PNTerm{T,PNOrder}(câ»Â¹exp::Int, coeff) where {T,PNOrder}
        if câ»Â¹exp > 2PNOrder
            coeff = zero(coeff)
        end
        new{T,PNOrder,câ»Â¹exp}(coeff)
    end
end

Base.length(pn::PNTerm) = 1
Base.eltype(pn::PNTerm{T}) where {T} = T
câ»Â¹exp(pn::PNTerm{T,PNOrder,câ»Â¹Exponent}) where {T,PNOrder,câ»Â¹Exponent} = câ»Â¹Exponent

function Base.sum(pn::PNTerm)
    pn.coeff
end

function Base.:+(pn::PNTerm)
    pn
end

function Base.inv(term::PNTerm{T,PNOrder,câ»Â¹Exponent}) where {T,PNOrder,câ»Â¹Exponent}
    PNTerm{T,PNOrder}(-câ»Â¹exp(term), inv(term.coeff))
end

function Base.:^(term::PNTerm{T,PNOrder,câ»Â¹Exponent}, n::Int) where {T,PNOrder,câ»Â¹Exponent}
    coeff = term.coeff^n
    PNTerm{typeof(coeff),PNOrder}(câ»Â¹exp(term) * n, coeff)
end

function Base.:*(x::Number, term::PNTerm{T,PNOrder,câ»Â¹Exponent}) where {T,PNOrder,câ»Â¹Exponent}
    coeff = x * term.coeff
    PNTerm{typeof(coeff),PNOrder,câ»Â¹Exponent}(coeff)
end
Base.:*(term::PNTerm, x::Number) = x * term

function Base.:/(term::PNTerm{T,PNOrder,câ»Â¹Exponent}, x::Number) where {T,PNOrder,câ»Â¹Exponent}
    coeff = term.coeff / x
    PNTerm{typeof(coeff),PNOrder,câ»Â¹Exponent}(coeff)
end

function Base.:/(x::Number, term::PNTerm{T,PNOrder,câ»Â¹Exponent}) where {T,PNOrder,câ»Â¹Exponent}
    coeff = x / term.coeff
    PNTerm{typeof(coeff),PNOrder}(-câ»Â¹exp(term), coeff)
end

function Base.:+(x::T1, term::PNTerm{T2,PNOrder,câ»Â¹Exponent}) where {T1<:Number,T2,PNOrder,câ»Â¹Exponent}
    if câ»Â¹exp(term) < 0
        throw(ArgumentError(
            "Cannot add a `PNTerm` with a negative exponent: "
            * "câ»Â¹exp(term)=$(câ»Â¹exp(term))."
            * "\nResult will be a `PNExpansion`, which cannot store positive exponents."
        ))
    end
    T = promote_type(T1, T2)
    Nâ‚€ = câ»Â¹exp(term) + 1
    NMax = Int(2PNOrder + 1)
    N = min(Nâ‚€, NMax)
    coeffs = _efficient_vector(Val(N), Val(T))
    coeffs .= zero(T)
    @inbounds coeffs[1] = x
    @inbounds if Nâ‚€ â‰¤ NMax
        coeffs[Nâ‚€] += term.coeff
    end
    PNExpansion{N,T,NMax}(Tuple(coeffs))
end
Base.:+(term::PNTerm, x::Number) = x + term

function Base.:-(term::PNTerm{T,PNOrder,câ»Â¹Exponent}) where {T,PNOrder,câ»Â¹Exponent}
    PNTerm{T,PNOrder,câ»Â¹Exponent}(-term.coeff)
end

function Base.:*(term1::PNTerm{T1,PNOrder,câ»Â¹E1}, term2::PNTerm{T2,PNOrder,câ»Â¹E2}) where
{T1,T2,PNOrder,câ»Â¹E1,câ»Â¹E2}
    câ»Â¹Exponent = câ»Â¹exp(term1) + câ»Â¹exp(term2)
    coeff = term1.coeff * term2.coeff
    PNTerm{typeof(coeff),PNOrder,câ»Â¹Exponent}(coeff)
end

function Base.:/(term1::PNTerm{T1,PNOrder,câ»Â¹E1}, term2::PNTerm{T2,PNOrder,câ»Â¹E2}) where
{T1,T2,PNOrder,câ»Â¹E1,câ»Â¹E2}
    câ»Â¹Exponent = câ»Â¹E1 - câ»Â¹E2
    coeff = term1.coeff / term2.coeff
    PNTerm{typeof(coeff),PNOrder,câ»Â¹Exponent}(coeff)
end

function Base.:+(term1::PNTerm{T1,PNOrder,câ»Â¹E1}, term2::PNTerm{T2,PNOrder,câ»Â¹E2}) where
{T1,T2,PNOrder,câ»Â¹E1,câ»Â¹E2}
    if câ»Â¹exp(term1) < 0
        throw(ArgumentError(
            "Cannot add a `PNTerm` with a negative exponent: "
            * "câ»Â¹exp(term1)=$(câ»Â¹exp(term1))."
            * "\nResult will be a `PNExpansion`, which cannot store positive exponents."
        ))
    end
    if câ»Â¹exp(term2) < 0
        throw(ArgumentError(
            "Cannot add a `PNTerm` with a negative exponent: "
            * "câ»Â¹exp(term2)=$(câ»Â¹exp(term2))."
            * "\nResult will be a `PNExpansion`, which cannot store positive exponents."
        ))
    end
    T = promote_type(T1, T2)
    N1â‚€ = câ»Â¹exp(term1) + 1
    N2â‚€ = câ»Â¹exp(term2) + 1
    NMax = Int(2PNOrder + 1)
    N = min(max(N1â‚€, N2â‚€), NMax)
    coeffs = _efficient_vector(Val(N), Val(T))
    coeffs .= zero(T)
    @inbounds if N1â‚€ â‰¤ N
        coeffs[N1â‚€] += term1.coeff
    end
    @inbounds if N2â‚€ â‰¤ N
        coeffs[N2â‚€] += term2.coeff
    end
    PNExpansion{N,T,NMax}(Tuple(coeffs))
end

Base.:-(term1::PNTerm, term2::PNTerm) = term1 + (-term2)

function Base.:+(term::PNTerm{T1,PNOrder,câ»Â¹E1}, expansion::PNExpansion{N2,T2,NMax2}) where
{T1,PNOrder,câ»Â¹E1,N2,T2,NMax2}
    if câ»Â¹exp(term) < 0
        throw(ArgumentError(
            "Cannot add a `PNTerm` with a negative exponent: "
            * "câ»Â¹exp(term)=$(câ»Â¹exp(term))."
            * "\nResult will be a `PNExpansion`, which cannot store positive exponents."
        ))
    end
    N1 = câ»Â¹exp(term) + 1
    NMax1 = Int(2PNOrder + 1)
    NMax = min(NMax1, NMax2)
    N = min(max(N1, N2), NMax)
    T = promote_type(T1, T2)
    coeffs = _efficient_vector(Val(N), Val(T))
    coeffs .= zero(T)
    @inbounds if N1 â‰¤ N
        coeffs[N1] += term.coeff
    end
    @inbounds for i âˆˆ 1:N
        if i â‰¤ N2
            coeffs[i] += expansion[i]
        end
    end
    PNExpansion{N,T,NMax}(Tuple(coeffs))
end
Base.:+(expansion::PNExpansion, term::PNTerm) = term + expansion

Base.:-(term::PNTerm, x::Number) = term + (-x)
Base.:-(x::Number, term::PNTerm) = x + (-term)
Base.:-(term::PNTerm, expansion::PNExpansion) = term + (-expansion)
Base.:-(expansion::PNExpansion, term::PNTerm) = expansion + (-term)
Base.:-(x::Number, expansion::PNExpansion) = x + (-expansion)
Base.:-(expansion::PNExpansion, x::Number) = expansion + (-x)

function Base.:*(expansion::PNExpansion{N1,T1,NMax1}, term::PNTerm{T2,PNOrder,câ»Â¹E2}) where
{N1,T1,NMax1,T2,PNOrder,câ»Â¹E2}
    Î”N = câ»Â¹exp(term)  # Note that Î”N may be negative!
    NMax2 = Int(2PNOrder + 1)
    NMax = min(NMax1, NMax2)
    N = min(max(N1, N1+Î”N), NMax)

    # Check that no terms from expansion will be lost to negative PN orders
    @inbounds for i âˆˆ 1:min(max(0,-Î”N), N1)
        if !iszero(expansion[i])
            throw(ArgumentError(
                "Cannot multiply `PNExpansion` by `PNTerm` with negative exponent: "
                * "câ»Â¹exp(term)=$(câ»Â¹exp(term))."
                * "\nResult will be a `PNExpansion`, which cannot store positive exponents."
            ))
        end
    end

    T = promote_type(T1, T2)
    coeffs = _efficient_vector(Val(N), Val(T))
    coeffs .= zero(T)
    @inbounds for i âˆˆ max(1,1-Î”N):min(N1,N-Î”N)
        coeffs[i+Î”N] = expansion[i] * term.coeff
    end
    PNExpansion{N,T,NMax}(Tuple(coeffs))
end
Base.:*(term::PNTerm, expansion::PNExpansion) = expansion * term
# (a, b, c, d, e, f, g) * (câ»Â¹^2) = (0, 0, a, b, c, d, e)

Base.:/(expansion::PNExpansion, term::PNTerm) = expansion * inv(term)


"""
    PNExpansionParameter(pnsystem)

Create a [`PNTerm`](@ref) object representing the post-Newtonian expansion parameter ``c``.
This can be used to automatically create more complicated `PNTerm`s, which combine to form a
[`PNExpansion`](@ref).  This is a simple but effective way to write PN formulas while
automatically tracking the PN order of each term.
"""
function PNExpansionParameter(::PNSystem{ST, PNOrder}) where {ST,PNOrder}
    PNTerm{eltype(ST), PNOrder}(-1, one(eltype(ST)))
end
