"""
    termination_forwards(v‚Çë, [quiet])

Construct termination criteria of solving PN evolution forwards in time

These criteria include checking that the masses are positive and the
dimensionless spins are less than 1, as well as ensuring that the evolution
will terminate at `v‚Çë`.

The optional `quiet` argument will silence informational messages about
reaching the target value of `v‚Çë` if set to `true`, but warnings will still be
issued when terminating for other reasons.  If you want to quiet warnings also,
you can do something like this:
```julia
using Logging
with_logger(SimpleLogger(Logging.Error)) do
    <your code goes here>
end
```
"""
function termination_forwards(v‚Çë, quiet=false)
    # Triggers the `continuous_terminator!` whenever one of these conditions crosses 0.
    # More precisely, the integrator performs a root find to finish precisely
    # when one of these conditions crosses 0.
    function conditions(out,state,t,integrator)
        out[1] = state[1]  # Terminate if M‚ÇÅ‚â§0
        out[2] = state[2]  # Terminate if M‚ÇÇ‚â§0
        out[3] = 1 - abs2vec(QuatVec{typeof(v‚Çë)}(state[3:5]...))  # Terminate if œá‚ÇÅ>1
        out[4] = 1 - abs2vec(QuatVec{typeof(v‚Çë)}(state[6:8]...))  # Terminate if œá‚ÇÇ>1
        out[5] = v‚Çë - state[13]  # Terminate at v = v‚Çë
    end
    function terminator!(integrator, event_index)
        if event_index == 1
            @warn "Terminating forwards evolution because M‚ÇÅ has become non-positive.  This is unusual."
        elseif event_index == 2
            @warn "Terminating forwards evolution because M‚ÇÇ has become non-positive.  This is unusual."
        elseif event_index == 3
            @warn "Terminating forwards evolution because œá‚ÇÅ>1.  Suggests early breakdown of PN."
        elseif event_index == 4
            @warn "Terminating forwards evolution because œá‚ÇÇ>1.  Suggests early breakdown of PN."
        elseif event_index == 5
            quiet || @info (
                "Terminating forwards evolution because the PN parameter ùë£ "
                * "has reached ùë£‚Çë=$(value(v‚Çë)).  This is ideal."
            )
        end
        terminate!(integrator)
    end
    VectorContinuousCallback(
        conditions,
        terminator!,
        5;  # We have 5 criteria above
        save_positions=(false,false)  # Only save before the termination, not after
    )
end


"""
    termination_backwards(v‚ÇÅ, [quiet])

Construct termination criteria of solving PN evolution backwards in time

These criteria include checking that the masses are positive and the
dimensionless spins are less than 1, as well as ensuring that the evolution
will terminate at `v‚ÇÅ`.

The optional `quiet` argument will silence informational messages about
reaching the target value of `v‚ÇÅ` if set to `true`, but warnings will still be
issued when terminating for other reasons.  If you want to quiet warnings also,
you can do something like this:
```julia
using Logging
with_logger(SimpleLogger(Logging.Error)) do
    <your code goes here>
end
```
"""
function termination_backwards(v‚ÇÅ, quiet=false)
    function terminators_backwards(out,state,t,integrator)
        out[1] = state[1]  # Terminate if M‚ÇÅ‚â§0
        out[2] = state[2]  # Terminate if M‚ÇÇ‚â§0
        out[3] = 1 - abs2vec(QuatVec{typeof(v‚ÇÅ)}(state[3:5]...))  # Terminate if œá‚ÇÅ>1
        out[4] = 1 - abs2vec(QuatVec{typeof(v‚ÇÅ)}(state[6:8]...))  # Terminate if œá‚ÇÇ>1
        out[5] = v‚ÇÅ - state[13]  # Terminate at v = v‚ÇÅ
    end
    function terminator_backwards!(integrator, event_index)
        if event_index == 1
            @warn "Terminating backwards evolution because M‚ÇÅ has become non-positive.  Suggests problem with PN."
        elseif event_index == 2
            @warn "Terminating backwards evolution because M‚ÇÇ has become non-positive.  Suggests problem with PN."
        elseif event_index == 3
            @warn "Terminating backwards evolution because œá‚ÇÅ>1.  Suggests problem with PN."
        elseif event_index == 4
            @warn "Terminating backwards evolution because œá‚ÇÇ>1.  Suggests problem with PN."
        elseif event_index == 5
            quiet || @info (
                "Terminating backwards evolution because the PN parameter ùë£ "
                * "has reached ùë£‚ÇÅ=$(value(v‚ÇÅ)).  This is ideal."
            )
        end
        terminate!(integrator)
    end
    VectorContinuousCallback(
        terminators_backwards,
        terminator_backwards!,
        5;  # We have 5 criteria above
        save_positions=(false,false)  # Only save before the termination, not after
    )
end


"""
    dtmin_terminator(T)

Construct termination criterion to terminate when `dt` drops below `‚àöeps(T)`.

Pass `force_dtmin=true` to `solve` when using this callback.  Otherwise, the
time-step size may decrease too much *within* a single time step, so that the
integrator itself will quit before reaching this callback, leading to a less
graceful exit.
"""
function dtmin_terminator(T)
    # Triggers the `discrete_terminator!` whenever this condition is true after
    # an integration step
    œµ = ‚àöeps(T)
    function discrete_condition(state,t,integrator)
        abs(integrator.dt) < œµ
    end
    function discrete_terminator!(integrator)
        @warn (
            "Terminating forwards evolution because time-step size is too small:\n"
            * "|dt=$(integrator.dt)| < ‚àöœµ=$(œµ)\n"
            * "This is probably fine if `v` ‚â≥ 1/2."
        )
        terminate!(integrator)
    end
    DiscreteCallback(
        discrete_condition,
        discrete_terminator!;
        save_positions=(false,false)
    )
end


"""
    nonfinite_terminator()

Construct termination criterion to terminate when any NaN or Inf is found in
the data after an integration step.
"""
function nonfinite_terminator()
    # Triggers the `discrete_terminator!` whenever this condition is true after
    # an integration step
    function discrete_condition(state,t,integrator)
        # any(isnan, state) || isnan(t) || isnan(integrator.dt)
        !(all(isfinite, state) && isfinite(t) && isfinite(integrator.dt))
    end
    function discrete_terminator!(integrator)
        @warn "Terminating forwards evolution because a non-finite number was found"
        terminate!(integrator)
    end
    DiscreteCallback(
        discrete_condition,
        discrete_terminator!;
        save_positions=(false,false)
    )
end
