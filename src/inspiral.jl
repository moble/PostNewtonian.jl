using Base: promote_typeof

"""
    value(x)

Return `x` or the value wrapped by the `Dual` number `x`

"""
value(x) = hasproperty(x, :value) ? getproperty(x, :value) : x


"""
    termination_forwards(vâ‚‘)

Construct termination criteria of solving PN evolution forwards in time

These criteria include checking that the masses are positive and the
dimensionless spins are less than 1, as well as ensuring that the evolution
will terminate at `vâ‚‘`.

"""
function termination_forwards(vâ‚‘)
    # Triggers the `continuous_terminator!` whenever one of these conditions crosses 0.
    # More precisely, the integrator performs a root find to finish precisely
    # when one of these conditions crosses 0.
    function conditions(out,u,t,integrator)
        out[1] = u[1]  # Terminate if Mâ‚â‰¤0
        out[2] = u[2]  # Terminate if Mâ‚‚â‰¤0
        out[3] = 1 - abs2vec(QuatVec{typeof(vâ‚‘)}(u[3:5]...))  # Terminate if Ï‡â‚>1
        out[4] = 1 - abs2vec(QuatVec{typeof(vâ‚‘)}(u[6:8]...))  # Terminate if Ï‡â‚‚>1
        out[5] = vâ‚‘ - u[end]  # Terminate at v = vâ‚‘
    end
    function terminator!(integrator, event_index)
        if event_index == 1
            @warn "Terminating forwards evolution because Mâ‚ has become non-positive.  This is unusual."
        elseif event_index == 2
            @warn "Terminating forwards evolution because Mâ‚‚ has become non-positive.  This is unusual."
        elseif event_index == 3
            @warn "Terminating forwards evolution because Ï‡â‚>1.  Suggests early breakdown of PN."
        elseif event_index == 4
            @warn "Terminating forwards evolution because Ï‡â‚‚>1.  Suggests early breakdown of PN."
        elseif event_index == 5
            @info (
                "Terminating forwards evolution because the PN parameter ğ‘£ "
                * "has reached ğ‘£â‚‘=$(value(vâ‚‘)).  This is ideal."
            )
        end
        terminate!(integrator)
    end
    VectorContinuousCallback(
        conditions,
        terminator!,
        5;  # We have 5 criteria above
        save_positions=(false,false)  # Only save before the termination, not after
    )
end


"""
    termination_backwards(vâ‚)

Construct termination criteria of solving PN evolution backwards in time

These criteria include checking that the masses are positive and the
dimensionless spins are less than 1, as well as ensuring that the evolution
will terminate at `vâ‚`.

"""
function termination_backwards(vâ‚)
    function terminators_backwards(out,u,t,integrator)
        out[1] = u[1]  # Terminate if Mâ‚â‰¤0
        out[2] = u[2]  # Terminate if Mâ‚‚â‰¤0
        out[3] = 1 - abs2vec(QuatVec{typeof(vâ‚)}(u[3:5]...))  # Terminate if Ï‡â‚>1
        out[4] = 1 - abs2vec(QuatVec{typeof(vâ‚)}(u[6:8]...))  # Terminate if Ï‡â‚‚>1
        out[5] = vâ‚ - u[end]  # Terminate at v = vâ‚
    end
    function terminator_backwards!(integrator, event_index)
        if event_index == 1
            @warn "Terminating backwards evolution because Mâ‚ has become non-positive.  Suggests problem with PN."
        elseif event_index == 2
            @warn "Terminating backwards evolution because Mâ‚‚ has become non-positive.  Suggests problem with PN."
        elseif event_index == 3
            @warn "Terminating backwards evolution because Ï‡â‚>1.  Suggests problem with PN."
        elseif event_index == 4
            @warn "Terminating backwards evolution because Ï‡â‚‚>1.  Suggests problem with PN."
        elseif event_index == 5
            @info (
                "Terminating backwards evolution because the PN parameter ğ‘£ "
                * "has reached ğ‘£â‚=$(value(vâ‚)).  This is ideal."
            )
        end
        terminate!(integrator)
    end
    VectorContinuousCallback(
        terminators_backwards,
        terminator_backwards!,
        5;  # We have 5 criteria above
        save_positions=(false,false)  # Only save before the termination, not after
    )
end


"""
    dtmin_terminator(T)

Construct termination criterion to terminate when `dt` drops below `âˆšeps(T)`.

Pass `force_dtmin=true` to `solve` when using this callback.  Otherwise, the
time-step size may decrease too much *within* a single time step, so that the
integrator itself will quit before reaching this callback, leading to a less
graceful exit.

"""
function dtmin_terminator(T)
    # Triggers the `discrete_terminator!` whenever this condition is true after
    # an integration step
    Ïµ = âˆšeps(T)
    function discrete_condition(u,t,integrator)
        abs(integrator.dt) < Ïµ
    end
    function discrete_terminator!(integrator)
        @info "Terminating forwards evolution because |dt=$(integrator.dt)| < Ïµ=$(Ïµ)"
        terminate!(integrator)
    end
    DiscreteCallback(
        discrete_condition,
        discrete_terminator!;
        save_positions=(false,false)
    )
end


"""
    nonfinite_terminator()

Construct termination criterion to terminate when any NaN or Inf is found in
the data after an integration step.

"""
function nonfinite_terminator()
    # Triggers the `discrete_terminator!` whenever this condition is true after
    # an integration step
    function discrete_condition(u,t,integrator)
        # any(isnan, u) || isnan(t) || isnan(integrator.dt)
        !(all(isfinite, u) && isfinite(t) && isfinite(integrator.dt))
    end
    function discrete_terminator!(integrator)
        @info "Terminating forwards evolution because a non-finite number was found"
        terminate!(integrator)
    end
    DiscreteCallback(
        discrete_condition,
        discrete_terminator!;
        save_positions=(false,false)
    )
end


"""
    inspiral(Mâ‚, Mâ‚‚, Ï‡âƒ—â‚, Ï‡âƒ—â‚‚, Î©áµ¢; kwargs...)

Integrate the orbital dynamics of a non-eccentric compact binary.


## Keyword arguments

  * `Î©â‚=Î©áµ¢`: First angular velocity in output data (see next section).
  * `Î©â‚‘=1`: Final angular velocity at which to stop ODE integration.
  * `Ráµ¢=Rotor(true)`: Initial orientation of binary.
  * `PNSys=TaylorT1`: Currently the only possibility.
  * `PNOrder=7//2`: Not actually used currently.
  * `check_up_down_instability=true`: Warn if the [Up-down instability](@ref)
    is likely to affect this system.
  * `time_stepper=AutoVern9(Rodas5())`: Choice of solver in OrdinaryDiffEq to
    integrate ODE.
  * `abstol=eps(T)^(11//16)`: Absolute tolerance of ODE solver, where `T` is
    the common type to which all the positional arguments are promoted.  This
    is the tolerance on local error estimates, not necessarily the global
    error.
  * `reltol=eps(T)^(11//16)`: Relative tolerance of ODE solver.  (As above.)
  * `termination_criteria_forwards=nothing`: Callbacks to `solve` for
    forwards-in-time evolution.  See below for discussion of the default value.
  * `termination_criteria_backwards=nothing`: Callbacks to `solve` for
    backwards-in-time evolution.  See below for discussion of the default value.
  * `force_dtmin=true`: If `dt` decreases below the integrator's own minimum,
    and this is false, the integrator will immediately raise an error, before
    the termination criteria have the chance to exit gracefully.  Note that a
    true value here is critical if the `dtmin_terminator` callback is to have
    any effect.

All remaining keyword arguments are passed to the [`solve`
function](https://github.com/SciML/DiffEqBase.jl/blob/8e6173029c630f6908252f3fc28a69c1f0eab456/src/solve.jl#L393)
of `DiffEqBase`.  See that function's documentation for details, including
useful keyword arguments.  The most likely important ones are

  * `saveat`: Denotes specific times to save the solution at, during the
    solving phase.
  * `dt`: Sets the *initial* stepsize. Defaults to an automatic choice if the
    method is adaptive.
  * `dtmax`: Maximum dt for adaptive timestepping.
  * `dtmin`: Minimum dt for adaptive timestepping.

Note that if you want the solution to be output with specifically spaced time
steps, you *don't* want `dt`, which is just the initial suggestion for adaptive
systems; you want to set `saveat` to the desired spacing.  [The `saveat`
argument could be a vector of specific times at which to save, but because we
don't know when the PN evolution ends, this probably isn't useful.]

Also note that `callback` is already used by this function (in addition to the
`abstol` and `reltol` mentioned above), which currently makes it impossible to
modify the callbacks.  Hacking will be required to change that.  (Note: I think
this may be easy, if we add the current callbacks to the *problem*, and just
pass any input callbacks to **`solve`**.  I haven't tested it though.)


## Initial frequency vs. first frequency vs. end frequency

Note the distinction between `Î©áµ¢` (with subscript `i`) and `Î©â‚` (with subscript
`1`).  The first, `Î©áµ¢`, represents the angular velocity of the *initial
condition* from which the ODE integrator will begin; the second, `Î©â‚`,
represents the target angular velocity of the first element of the output data.
That is, the ODE integration will run forwards in time from `Î©áµ¢` to the merger,
and then come back to `Î©áµ¢` and run backwards in time to `Î©â‚`.  The output data
will stitch these two together to be one continuous (forwards-in-time) data
series.

For example, if you are trying to match to a numerical relativity (NR)
simulation, you can read the masses and spins off of the NR data when the
system is orbiting at angular velocity `Î©áµ¢`.  Integrating the post-Newtonian
(PN) solution forwards in time from this point will allow you to compare the PN
and NR waveforms.  However, you may want to know what the waveform was at
*earlier* times than are present in the NR data.  For this, you also have to
integrate backwards in time.  We parametrise the point to which you integrate
backwards with `Î©â‚`.  In either case, element `1` of the output solution will
have frequency `Î©â‚` â€” though by default it is equal to `Î©áµ¢`.

Similarly, the optional argument `Î©â‚‘=1` is the frequency of the `end` element
of the solution â€” that is Julia's notation for the last element.  Note that
this is automatically reduced if necessary so that the corresponding PN
parameter ``v`` is no greater than 1, which may be the case whenever the total
mass is greater than 1.


## Up-down instability

Be aware that the [up-down instability](http://arxiv.org/abs/1506.09116) (where
the more massive black hole has spin aligned with the orbital angular velocity,
and the less massive has spin anti-aligned) can cause systems with nearly zero
precession at the initial time to evolve into a highly precessing system either
at earlier or later times.  This is a real physical result, rather than a
numerical issue.  If you want to simulate a truly non-precessing system, you
should explicitly set the in-place components of spin to precisely 0.  By
default, we check for this condition, and will issue a warning if it is likely
to be encountered for systems with low initial precession.  The function used
to compute the unstable region is [`up_down_instability`](@ref).


## Time-stepper algorithms

`Tsit5()` is a good default choice for time stepper when using `Float64` with
medium-low tolerance.  If stiffness seems to be impacting the results,
`AutoTsit5(Rosenbrock23())` will automatically switch when stiffness occurs.
For tighter tolerances, especially when using `Double64`s, `Vern9()` or
`AutoVern9(Rodas5())` are good choices.  For very loose tolerances, as when
using `Float32`s, it might be better to use `OwrenZen3()`.


## Termination criteria

The termination criteria are vital to efficiency of the integration and
correctness of the solution.  The default values for forwards- and
backwards-in-time evolution, respectively, are
```julia
CallbackSet(
    termination_forwards(v(Î©=Î©â‚‘, M=Mâ‚+Mâ‚‚)),
    dtmin_terminator(T),
    nonfinite_terminator()
)
```
and
```julia
CallbackSet(
    termination_backwards(v(Î©=Î©â‚, M=Mâ‚+Mâ‚‚)),
    dtmin_terminator(T),
    nonfinite_terminator()
)
```
where `T` is the common float type of the input arguments.  If any additional
termination criteria are needed, they could be added as additional elements of
the `CallbackSet`s.  See the [callback
documentation](https://diffeq.sciml.ai/stable/features/callback_functions/) for
details.

"""
function inspiral(
    Mâ‚, Mâ‚‚, Ï‡âƒ—â‚, Ï‡âƒ—â‚‚, Î©áµ¢;
    Î©â‚=Î©áµ¢, Î©â‚‘=1, Ráµ¢=Rotor(true),
    PNSys=TaylorT1, PNOrder=7//2,
    check_up_down_instability=true, time_stepper=AutoVern9(Rodas5()),
    reltol=nothing, abstol=nothing,
    termination_criteria_forwards=nothing,
    termination_criteria_backwards=nothing,
    force_dtmin=true,
    solve_kwargs...
)
    if Î©â‚ > Î©áµ¢
        error(
            "Initial frequency Î©áµ¢=$Î©áµ¢ should be greater than or equal to first frequency Î©â‚=$Î©â‚"
        )
    end

    váµ¢ = v(Î©=Î©áµ¢, M=Mâ‚+Mâ‚‚)
    if váµ¢ â‰¥ 1
        error(
            "The input Î©áµ¢=$Î©áµ¢ is too large; with these masses, it corresponds to "
            * "váµ¢=$váµ¢, which is beyond the reach of post-Newtonian methods."
        )
    end

    vâ‚ = v(Î©=Î©â‚, M=Mâ‚+Mâ‚‚)
    vâ‚‘ = min(v(Î©=Î©â‚‘, M=Mâ‚+Mâ‚‚), 1)

    # Initial conditions for the ODE integration
    uáµ¢ = [Mâ‚; Mâ‚‚; Ï‡âƒ—â‚.components[2:4]; Ï‡âƒ—â‚‚.components[2:4]; Ráµ¢.components; váµ¢]
    # We pack this up here, to get everything into the same type, and permit easier
    # passing to the other form of this function; we'll unpack it again there,
    # which makes sure everything has the same type and the function is type stable.

    T = eltype(uáµ¢)
    if isnothing(reltol)
        reltol = eps(T)^(11//16)
    end
    if isnothing(abstol)
        abstol = eps(T)^(11//16)
    end

    inspiral(
        uáµ¢,
        vâ‚, vâ‚‘,
        PNSys(PNOrder, T), T,
        check_up_down_instability, time_stepper,
        reltol, abstol,
        termination_criteria_forwards,
        termination_criteria_backwards,
        force_dtmin;
        solve_kwargs...
    )
end

function inspiral(
    uáµ¢, vâ‚, vâ‚‘,
    pn::PNSystem, T,
    check_up_down_instability, time_stepper,
    reltol, abstol,
    termination_criteria_forwards,
    termination_criteria_backwards,
    force_dtmin;
    solve_kwargs...
)

    Mâ‚, Mâ‚‚, Ï‡âƒ—â‚Ë£, Ï‡âƒ—â‚Ê¸, Ï‡âƒ—â‚á¶», Ï‡âƒ—â‚‚Ë£, Ï‡âƒ—â‚‚Ê¸, Ï‡âƒ—â‚‚á¶», RÊ·, RË£, RÊ¸, Rá¶», váµ¢ = uáµ¢
    Ï‡âƒ—â‚ = QuatVec(Ï‡âƒ—â‚Ë£, Ï‡âƒ—â‚Ê¸, Ï‡âƒ—â‚á¶»)
    Ï‡âƒ—â‚‚ = QuatVec(Ï‡âƒ—â‚‚Ë£, Ï‡âƒ—â‚‚Ê¸, Ï‡âƒ—â‚‚á¶»)
    R = Quaternion(RÊ·, RË£, RÊ¸, Rá¶»)

    if check_up_down_instability
        Ï‡â‚šâ‚‘áµ£â‚š = let nÌ‚=nÌ‚(R), Î»Ì‚=Î»Ì‚(R)
            âˆš((Ï‡âƒ—â‚ â‹… nÌ‚)^2 + (Ï‡âƒ—â‚ â‹… Î»Ì‚)^2 + (Ï‡âƒ—â‚‚ â‹… nÌ‚)^2 + (Ï‡âƒ—â‚‚ â‹… Î»Ì‚)^2)
        end
        if Ï‡â‚šâ‚‘áµ£â‚š â‰¤ 1e-2
            (Î©â‚Š, Î©â‚‹) = up_down_instability(uáµ¢)
            if Î©â‚ < Î©â‚‹ < 1//4 || Î©â‚ < Î©â‚Š < 1//4
                @warn (
                    "This system is likely to encounter the up-down instability in the\n"
                    * "frequency range (Î©â‚Š, Î©â‚‹)=$((Î©â‚Š, Î©â‚‹)).\n"
                    * "This is a true physical instability; not just a numerical issue.\n"
                    * "Despite the initial conditions containing very small precession,\n"
                    * "the system will likely evolve to have very large precession."
                )
            end
        end
    end

    estimated_time_to_merger = 5/(256Î½(Mâ‚, Mâ‚‚) * T(váµ¢)^8) # Lowest-order PN time-to-merger
    tspan = (T(0), 4estimated_time_to_merger)
    problem_forwards = ODEProblem(noneccentric_RHS!, uáµ¢, tspan, pn)
    if isnothing(termination_criteria_forwards)
        termination_criteria_forwards = CallbackSet(
            termination_forwards(vâ‚‘),
            dtmin_terminator(T),
            nonfinite_terminator()
        )
    end

    # Log an error if the initial parameters return a NaN on the right-hand side
    let
        uÌ‡ = similar(uáµ¢)
        noneccentric_RHS!(uÌ‡, uáµ¢, pn, tspan[1])
        if any(isnan, uÌ‡) ||  any(isnan, uáµ¢) ||  any(isnan, tspan)
            @error "Found a NaN with initial parameters:" value.(uáµ¢) value.(uÌ‡) pn value.(tspan)
            error("Found NaN")
        end
    end

    solution_forwards = solve(
        problem_forwards, time_stepper;
        callback=termination_criteria_forwards,
        reltol=reltol, abstol=abstol,
        force_dtmin=force_dtmin,
        solve_kwargs...
    )

    if vâ‚ < váµ¢
        estimated_backwards_time = 5/(256Î½(Mâ‚, Mâ‚‚) * T(vâ‚)^8) - estimated_time_to_merger
        tspan = (T(0), -3estimated_backwards_time)
        problem_backwards = remake(problem_forwards; tspan=tspan)
        if isnothing(termination_criteria_backwards)
            termination_criteria_backwards = CallbackSet(
                termination_backwards(vâ‚),
                dtmin_terminator(T),
                nonfinite_terminator()
            )
        end

        solution_backwards = solve(
            problem_backwards, time_stepper;
            callback=termination_criteria_backwards,
            reltol=reltol, abstol=abstol,
            force_dtmin=force_dtmin,
            solve_kwargs...
        )

        combine_solutions(solution_backwards, solution_forwards)
    else
        solution_forwards
    end
end


"""
    noneccentric_RHS!(uÌ‡, u, p, t)

Compute the right-hand side for the orbital evolution of a non-eccentric binary

Here, `u` is the ODE state vector, which can be unpacked with
[`PNDynamicalVariables`](@ref).  The parameter `p` is currently unused, but
could be used to pass un-evolved parameters through.

"""
function noneccentric_RHS!(uÌ‡, u, pn, t)
    recalculate!(uÌ‡, u, pn)
    nothing
end
